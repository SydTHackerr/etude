apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.andrewthehan"
archivesBaseName = "etude"
version = "2.0.0"

repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required {
    boolean signed = gradle.taskGraph.hasTask("uploadArchives")
  }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'etude'
        packaging 'jar'
        description 'Java Music Theory API'
        url 'https://github.com/andrewthehan/etude'

        scm {
          connection 'scm:git:git://github.com/andrewthehan/etude.git'
          developerConnection 'scm:git:ssh://github.com:andrewthehan/etude.git'
          url 'http://github.com/andrewthehan/etude/tree/master'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
          }
        }

        developers {
          developer {
            id 'andrewthehan'
            name 'Andrew Han'
            email 'andrewthehan@gmail.com'
          }
        }
      }
    }
  }
}